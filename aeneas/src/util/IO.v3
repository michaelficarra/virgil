// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// An abstraction for a stream which accepts bytes in large chunks.
class Stream {
	def outr(data: Range<byte>);
}
// A closure over a file descriptor which allows it to act as a stream.
class FileStream(fd: int) extends Stream {
	def outr(data: Range<byte>) {
		System.write(fd, data);
	}
	def close() {
		System.fileClose(fd);
	}
}
// A utility to buffer writes of bytes to a flushing function, with the assumption
// that the flushing function is more efficient with larger writes.
class Buffer {
	private def bufSize: int;
	private def ffunc: Range<byte> -> void;
	def var writer: DataWriter;
	private var flushed: int;

	// Create a new buffer with {bufSize} bytes which flushes to {ffunc}.
	new(bufSize, ffunc) {
		var array = Array<byte>.new(bufSize);
		writer = DataWriter.new().reset(array, 0, 0);
		writer.refill = refill;
	}
	// Refill the {writer} with {size} bytes when full.
	private def refill(writer: DataWriter, size: int) -> DataWriter {
		if (size > writer.data.length) {
			// a larger buffer is necessary
			var ndata = Array<byte>.new(writer.data.length + size);
			var e = writer.end();
			for (i < e) ndata[i] = writer.data[i];
			writer.reset(ndata, writer.pos, e);
		} else {
			// current buffer will suffice, but need to flush it
			flush();
		}
		return writer;
	}
	// Flush any remaining data to the flush function.
	def flush() {
		flushed += writer.end();
		writer.send(ffunc);
		writer.rewind(0);
	}
	// Get the writer which can output to this buffer.
	def getWriter() -> DataWriter {
		return writer;
	}
	// Get the total number of bytes output to this buffer.
	def getTotal() -> int {
		return flushed + writer.end();
	}
}
